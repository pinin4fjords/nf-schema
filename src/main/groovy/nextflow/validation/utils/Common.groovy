package nextflow.validation.utils

import org.json.JSONObject
import org.json.JSONArray
import org.json.JSONPointer
import org.json.JSONPointerException
import groovy.util.logging.Slf4j
import groovy.transform.CompileDynamic
import java.nio.file.Path
import java.util.stream.IntStream

/**
 * A collection of commonly used functions
 *
 * @author : mirpedrol <mirp.julia@gmail.com>
 * @author : nvnieuwk <nicolas.vannieuwkerke@ugent.be>
 * @author : KevinMenden
 */

@Slf4j
@CompileDynamic
public class Common {

    //
    // Get full path based on the base directory of the pipeline run
    //
    static String getBasePath(String baseDir, String schemaFilename) {
        if (Path.of(schemaFilename).exists()) {
            return schemaFilename
        }
        return "${baseDir}/${schemaFilename}"
    }

    // A schema referenced from another schema is looked up next to the schema that references it first,
    // then relative to the project, so that a schema keeps working when its pipeline is run from elsewhere
    static String getReferencedSchemaPath(String schemaDir, String baseDir, String schemaFilename) {
        if (schemaDir && !Path.of(schemaFilename).isAbsolute() && Path.of(schemaDir, schemaFilename).exists()) {
            return Path.of(schemaDir, schemaFilename).toString()
        }
        return getBasePath(baseDir, schemaFilename)
    }

    //
    // Function to get the value from a JSON pointer
    //
    static Object getValueFromJsonPointer(String jsonPointer, Object json) {
        JSONPointer schemaPointer = new JSONPointer(jsonPointer)
        try {
            return schemaPointer.queryFrom(json) ?: ''
        } catch (JSONPointerException e) {
            return ''
        }
    }

    //
    // Get the amount of character of the largest key in a map
    //
    static Integer getLongestKeyLength(Map input) {
        return Collections.max(input.collect { key, val ->
            Map groupParams = val as Map
            longestStringLength(groupParams.keySet() as List<String>)
        })
    }

    //
    // Get the size of the longest string value in a list of strings
    //
    static Integer longestStringLength(List<String> strings) {
        return strings ? Collections.max(strings*.size()) : 0
    }

    //
    // Find a value in a nested map
    //
    static Object findDeep(Object m, String key) {
        if (m in Map) {
            if (m.containsKey(key)) {
                return m[key]
            }
            return m.findResult { k, v -> findDeep(v, key) }
        }
        else if (m in List) {
            return m.findResult { element -> findDeep(element, key) }
        }
        return null
    }

    //
    // Check if a key exists in a nested map
    //
    static boolean hasDeepKey(Object m, String key) {
        if (m in Map) {
            if (m.containsKey(key)) {
                return true
            }
            return m.any { k, v -> hasDeepKey(v, key) }
        }
        else if (m in List) {
            return m.any { element -> hasDeepKey(element, key) }
        }
        return false
    }

    static void findAllKeys(Object object, String key, Set<String> finalKeys, String sep) {
        if (object in JSONObject) {
            JSONObject jsonObject = (JSONObject) object

            jsonObject.keySet().forEach { childKey ->
                findAllKeys(jsonObject.get(childKey), key != null ? key + sep + childKey : childKey, finalKeys, sep)
            }
        } else if (object in JSONArray) {
            JSONArray jsonArray = (JSONArray) object
            key != null ? finalKeys.add(key) : ''

            IntStream.range(0, jsonArray.length())
                    .mapToObj(jsonArray::get)
                    .forEach { jObj -> findAllKeys(jObj, key, finalKeys, sep) }
        }
        else {
            key != null ? finalKeys.add(key) : ''
        }
    }

    static String kebabToCamel(String s) {
        Closure<Object[]> toUpper = { Object[] strs -> strs[2].toUpperCase() }
        return s.replaceAll('(-)([A-Za-z0-9])', toUpper)
    }

    // Matched by package because the dataflow classes are not on the plugin's compile classpath
    static boolean isDataflowValue(Object value) {
        String className = value?.getClass()?.name ?: ''
        return className.startsWith('groovyx.gpars.dataflow.') || className.startsWith('nextflow.dataflow.')
    }

    // Channel and Value params hold live dataflow objects: reading them blocks, and they print as object
    // names. The value they were created from (given on the command line, else set in the config) is used
    // in their place, and a param without one is left out. Params nested in a record (e.g. the params of an
    // included pipeline) are handled the same way.
    static Map replaceDataflowParams(Map params, Object cliParams, Object configParams) {
        return replaceDataflowValues(params, cliParams, configParams) as Map
    }

    private static Object replaceDataflowValues(Object value, Object cliValue, Object configValue) {
        if (isDataflowValue(value)) {
            Object source = cliValue != null ? cliValue : configValue
            return source != null && !isDataflowValue(source) ? source : null
        }
        if (value instanceof Map) {
            Map<Object, Object> result = [:]
            (value as Map<Object, Object>).each { Object name, Object entry ->
                Object replaced = replaceDataflowValues(
                    entry,
                    cliValue instanceof Map ? (cliValue as Map)[name] : null,
                    configValue instanceof Map ? (configValue as Map)[name] : null
                )
                if (replaced != null || !isDataflowValue(entry)) {
                    result[name] = replaced
                }
            }
            return result
        }
        return value
    }

}
