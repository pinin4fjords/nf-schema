/* groovylint-disable LineLength */
package nextflow.validation.validators

import static nextflow.validation.utils.Common.getValueFromJsonPointer

import groovy.util.logging.Slf4j
import groovy.transform.CompileDynamic
import org.json.JSONObject
import java.nio.file.Files
import java.nio.file.Path
import dev.harrel.jsonschema.ValidatorFactory
import dev.harrel.jsonschema.Validator
import dev.harrel.jsonschema.EvaluatorFactory
import dev.harrel.jsonschema.FormatEvaluatorFactory
import dev.harrel.jsonschema.JsonNode
import dev.harrel.jsonschema.providers.OrgJsonNode

import nextflow.validation.config.ValidationConfig
import nextflow.validation.exceptions.SchemaValidationException
import nextflow.validation.validators.evaluators.CustomEvaluatorFactory

/**
 * The JSON schema validator
 *
 * @author : nvnieuwk <nicolas.vannieuwkerke@ugent.be>
 */

@Slf4j
@CompileDynamic
public class JsonSchemaValidator {

    final private ValidatorFactory validator
    final private CustomEvaluatorFactory customEvaluators
    final private ValidationConfig config

    JsonSchemaValidator(ValidationConfig config) {
        this.customEvaluators = new CustomEvaluatorFactory(config)
        this.validator = new ValidatorFactory()
            .withJsonNodeFactory(new OrgJsonNode.Factory())
            // .withDialect() // TODO define the dialect
            .withEvaluatorFactory(
                EvaluatorFactory.compose(this.customEvaluators, new FormatEvaluatorFactory())
            )
        this.config = config
    }

    ValidationResult validate(Object input, String schemaFileName) {
        JsonNode jsonInput = new OrgJsonNode.Factory().wrap(input)
        return validateObject(jsonInput, input, schemaFileName)
    }

    private ValidationResult validateObject(JsonNode input, Object rawJson, String schemaFileName) {
        JSONObject schema
        String schemaString
        try {
            schemaString = Files.readString(Path.of(schemaFileName))
            schema = new JSONObject(schemaString)
        } catch (org.json.JSONException e) {
            throw new SchemaValidationException("""Failed to load JSON schema (${schemaFileName}):
    ${e.message}

""")
        }

        String draft = getValueFromJsonPointer("#/\$schema", schema)
        if (draft != 'https://json-schema.org/draft/2020-12/schema') {
            log.error("""Failed to load the meta schema:
    The used schema draft (${draft}) is not correct, please use \"https://json-schema.org/draft/2020-12/schema\" instead.
        - If you are a pipeline developer, check our migration guide for more information: https://nextflow-io.github.io/nf-schema/latest/migration_guide/
        - If you are a pipeline user, revert back to nf-validation to avoid this error: https://www.nextflow.io/docs/latest/plugins.html#using-plugins, i.e. set `plugins {
    id 'nf-validation@1.1.3'
}` in your `nextflow.config` file
            """)
            throw new SchemaValidationException('', [])
        }
        this.customEvaluators.schemaDir = Path.of(schemaFileName).toAbsolutePath().parent?.toString()
        Validator.Result result = this.validator.validate(schema, input)
        return new ValidationResult(result, rawJson, schemaString, this.config)
    }

}
