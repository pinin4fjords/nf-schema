/* groovylint-disable LineLength, TrailingWhitespace, MethodName, UnnecessaryGString */
package nextflow.validation

import groovy.transform.CompileDynamic

import java.nio.file.Path

import nextflow.plugin.Plugins
import nextflow.plugin.TestPluginDescriptorFinder
import nextflow.plugin.TestPluginManager
import nextflow.plugin.extension.PluginExtensionProvider
import org.junit.Rule
import org.pf4j.PluginDescriptorFinder
import spock.lang.Shared
import spock.lang.Timeout
import test.Dsl2Spec
import test.OutputCapture
import test.MockScriptRunner

import java.nio.file.Files
import java.util.jar.Manifest

/**
 * @author : mirpedrol <mirp.julia@gmail.com>
 * @author : nvnieuwk <nicolas.vannieuwkerke@ugent.be>
 * @author : KevinMenden
 */

@CompileDynamic
class ParamsSummaryLogTest extends Dsl2Spec {

    @Rule
    final private OutputCapture capture = new OutputCapture()

    @Shared 
    private String pluginsMode

    final private Path root = Path.of('.').toAbsolutePath().normalize()

    void setup() {
        // reset previous instances
        PluginExtensionProvider.reset()
        // this need to be set *before* the plugin manager class is created
        pluginsMode = System.getProperty('pf4j.mode')
        System.setProperty('pf4j.mode', 'dev')
        // the plugin root should
        TestPluginManager manager = new TestPluginManager(root){

            @Override
            protected PluginDescriptorFinder createPluginDescriptorFinder() {
                return new TestPluginDescriptorFinder(){

                    @Override
                    protected Manifest readManifestFromDirectory(Path pluginPath) {
                        Path manifestPath = getManifestPath(pluginPath)
                        InputStream input = Files.newInputStream(manifestPath)
                        return new Manifest(input)
                    }
                    protected Path getManifestPath(Path pluginPath) {
                        return pluginPath.resolve('build/tmp/jar/MANIFEST.MF')
                    }

                }
            }

        }
        Plugins.init(root, 'dev', manager)
    }

    void cleanup() {
        Plugins.stop()
        PluginExtensionProvider.reset()
        pluginsMode ? System.setProperty('pf4j.mode', pluginsMode) : System.clearProperty('pf4j.mode')
    }

    void 'should print params summary'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema.json').toAbsolutePath()
        String script = """
            params.outdir = "outDir"
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        Map config = [:]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line ->
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('Input/output options') ||
                    line.contains('outdir')
                    ? line : null
                }

        then:
        noExceptionThrown()
        stdout.size() == 11
        stdout ==~ /.*outdir     : outDir.*/
    }

    @Timeout(60)
    void 'should print params summary - a param that holds a dataflow value is left out'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema.json').toAbsolutePath()
        String script = """
            params.outdir = "outDir"
            params.input = new groovyx.gpars.dataflow.DataflowVariable()
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        new MockScriptRunner([:]).setScript(script).execute()
        String stdout = capture.toString()

        then:
        stdout.contains('outdir')
        !stdout.contains('DataflowVariable')
        !stdout.contains('input ')
    }

    void 'should print params summary - nested parameters'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema_nested_parameters.json').toAbsolutePath()
        String script = """
            params.this.is.so.deep = "changed_value"
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        Map config = [
            'params': [
                'this': [
                    'is': [
                        'so': [
                            'deep': true
                        ]
                    ]
                ]
            ]
        ]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line ->
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('Nested Parameters') ||
                    line.contains('this.is.so.deep')
                    ? line : null
                }

        then:
        noExceptionThrown()
        stdout.size() == 11
        stdout ==~ /.*this.is.so.deep: changed_value.*/
    }

    void 'should print params summary - adds before and after text'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema.json').toAbsolutePath()
        String script = """
            params.outdir = "outDir"
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        Map config = [
            'validation': [
                'summary': [
                    'beforeText': "This text is printed before \n",
                    'afterText': "\nThis text is printed after",
                ]
            ]
        ]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line -> !line.contains('DEBUG') && !line.contains('after]]') ? line : null }
                .findResults { line ->
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('Input/output options') ||
                    line.contains('outdir') ||
                    line.contains('This text is printed before') ||
                    line.contains('This text is printed after')
                    ? line : null
                }
        then:
        noExceptionThrown()
        stdout.size() == 14
        stdout ==~ /.*outdir     : outDir.*/
    }

    void 'should print params summary - adds before and after text via arguments'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema.json').toAbsolutePath()
        String script = """
            params.outdir = 'outDir'
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(
                workflow,
                parameters_schema: '${schema}',
                beforeText: "This text is printed before \\n",
                afterText: "\\nThis text is printed after"
            )
            log.info summary_params
        """

        when:
        Map config = [:]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line -> !line.contains('DEBUG') && !line.contains('after]]') ? line : null }
                .findResults { line ->
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('Input/output options') ||
                    line.contains('outdir') ||
                    line.contains('This text is printed before') ||
                    line.contains('This text is printed after')
                    ? line : null 
                }

        then:
        noExceptionThrown()
        stdout.size() == 13
        stdout ==~ /.*outdir     : outDir.*/
    }

    void 'should print params summary - nested parameters - hide params'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema_nested_parameters.json').toAbsolutePath()
        String script = """
            params.this.is.so.deep = "changed_value"
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        Map config = [
            'params': [
                'this': [
                    'is': [
                        'so': [
                            'deep': true
                        ]
                    ]
                ]
            ],
            'validation': [
                'summary': [
                    'hideParams': ['params.this.is.so.deep']
                ]
            ]
        ]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line -> 
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('Nested Parameters') ||
                    line.contains('this.is.so.deep ')
                    ? line : null
                }

        then:
        noExceptionThrown()
        stdout.size() == 10
        stdout != ~ /.*this.is.so.deep: changed_value.*/
    }

    void 'should print params summary - hide params'() {
        given:
        String schema = Path.of('src/testResources/nextflow_schema.json').toAbsolutePath()
        String script = """
            params.outdir = "outDir"
            include { paramsSummaryLog } from 'plugin/nf-schema'

            def summary_params = paramsSummaryLog(workflow, parameters_schema: '$schema')
            log.info summary_params
        """

        when:
        Map config = [
            'validation': [
                'summary': [
                    'hideParams': ['outdir']
                ]
            ]
        ]
        new MockScriptRunner(config).setScript(script).execute()
        List<String> stdout = capture
                .toString()
                .readLines()
                .findResults { line ->
                    line.contains('Only displaying parameters that differ from the pipeline defaults') ||
                    line.contains('Core Nextflow options') ||
                    line.contains('runName') ||
                    line.contains('launchDir') ||
                    line.contains('workDir') ||
                    line.contains('projectDir') ||
                    line.contains('userName') ||
                    line.contains('profile') ||
                    line.contains('configFiles') ||
                    line.contains('outdir ')
                    ? line : null
                }

        then:
        noExceptionThrown()
        stdout.size() == 9
        stdout != ~ /.*outdir     : outDir.*/
    }

}
